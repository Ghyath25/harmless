semantics gadl_core :
  import "gadl_options.gOption" ;
  import "gadl_memory.gSemantics";
  import "gadl_isa.gSemantics";
  import "gadl_varIdfStruct.gSemantics";
  import "gadl_templates.gSemantics" ;
  import "gadl_semantics_templateExport.gSemantics" ;
  import "gadl_micro_architecture.gSemantics" ;
  import "gadl_timing_parser.gSyntax";

#key is core name.
map @ASTCoreMap
{
  @ASTIsa ASTIsa;
  @ASTmemoryParamMap ASTmemChunk;
  @stringlist memChunkOrder;
  @architectureMap archMap;
  @pipelineMap pipelineMap;
  @machineMap machineMap;
  @ASTFunctionMap ASTCoreConstructorMap;
  insert insertKey error message "the '%K' core is already defined in %L" ; 
  search searchKey error message "the '%K' core is not defined" ;
}

method @ASTCoreMap coreSemantic
  ?? @lstring exportDir
  ?? @memSpaceMap memSpaceMap
  ?! @TfieldMap templateStruct
:
  @TfieldMapList coreTpl [emptyList];
  foreach selfcopy do
    @TfieldMap coreTplBody [emptyMap];
    @lstring coreName := lkey;
    addLStringValue !?coreTplBody !"NAME" !coreName;
    ###########################################################################
    # memory space.
    ###########################################################################
    @varIdfStruct idfStruct [default];
    idfStruct->memSpaceMap := memSpaceMap;
    #message [idfStruct string];

    ###########################################################################
    #constructor of the core (memory defined, and ISA not yet).
    ###########################################################################
    @expressionContext ctx := [@expressionContext component]; #same ctx as a component...  
    @functionMap constructorMap := [ASTCoreConstructorMap getFunctionMap !ctx !idfStruct];
    addListValue !?coreTplBody !lstringWith[!"coreConstructorList"] 
                 ![constructorMap getFunctionTemplate];
    
    ###########################################################################
    # ISA
    ###########################################################################
    [ASTIsa getIsa !?idfStruct !coreName !exportDir !?coreTplBody ?@Isa isa];

    ###########################################################################
    # local memory definition
    ###########################################################################
    @memoryParamMap memChunk := [ASTmemChunk getMemMap];
    [memChunk setInTemplate !lstringWith[!"memInCoreList"] !?coreTplBody];

    ###########################################################################
    # Architecture (defined in core), for a CAS version.
    ###########################################################################
    @intMap memSpaceWithRegister := [memChunk memSpaceWithRegisterBank !memSpaceMap];
    microArchPart !isa !coreName !exportDir !machineMap !pipelineMap !archMap !idfStruct 
                  !memSpaceWithRegister !memChunk !?coreTplBody;

    ###########################################################################
    # export data from idfStruct (components and so on).
    ###########################################################################
    [idfStruct setInTemplate !?coreTplBody];
    [idfStruct setProgramCounterInTemplate !?coreTplBody];

    #end.
    coreTpl += !coreTplBody;
  end foreach;
  addListValue !?templateStruct !lstringWith[!"coreList"] !coreTpl;
end method;

end semantics ;
# vim:ft=ggs:ts=2:sw=2
